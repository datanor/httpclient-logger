plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.18.0'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'org.owasp.dependencycheck' version '8.2.1'
    id 'com.github.spotbugs' version '5.0.14'
    id 'com.github.ben-manes.versions' version '0.46.0'
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

group = 'ee.datanor.httpclient.logger'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'httpclient-logger'
                description = 'Access logger for HttpClient 5'
                url = 'https://github.com/datanor/httpclient-logger'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'priittalisoo'
                        name = 'Priit Talisoo'
                        email = 'priit.talisoo@datanor.ee'
                    }
                }
                scm {
                    url = 'https://github.com/datanor/httpclient-logger'
                    connection = 'scm:git://github.com/datanor/httpclient-logger.git'
                    developerConnection = 'scm:git://github.com/datanor/httpclient-logger.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

ext.genOutputDir = file("$buildDir/generated-resources")

task generateVersionTxt()  {
    ext.outputFile = file("$genOutputDir/version.txt")
    outputs.file(outputFile)
    doLast {
        outputFile.text = """GroupId: ${project.group}
Name: ${project.name}
Version: $version
Build-time: ${java.time.LocalDateTime.now()}
"""
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: generateVersionTxt

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.20.0'
    testImplementation "com.github.tomakehurst:wiremock-standalone:3.0.0-beta-2"

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.compilerArgs.addAll([
            '-Xlint:all', '-Werror',
            '-Xlint:-deprecation',
            '-Xlint:-processing',
            '-Xlint:-classfile'
    ])
}

checkstyle {
    ignoreFailures = false
    maxWarnings = 0
}

checkstyleMain {
    source = fileTree('src/main/java')
}

spotbugs {
    reportLevel = 'high'
    includeFilter = new File("${rootDir}/config/spotbugs/includeFilter.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
    }
}
